buildscript {
    ext.kotlin_version = '1.2.51'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2"
        classpath "com.tgirard12:gradle-step-release-plugin:0.5.0"
    }
}

group = project_group
version = project_version

apply plugin: 'kotlin'
apply from: "gradle/publish.gradle"
apply plugin: "com.tgirard12.gradle-step-release-plugin"


sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation gradleApi()

    testImplementation "io.kotlintest:kotlintest-runner-junit5:3.1.8"
}

test {
    useJUnitPlatform()
}

repositories {
    jcenter()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

releaseStep {

    githubGroup = "tgirard12"
    githubProject = "gradle-step-release-plugin"

    def gradleFile = "gradle.properties"
    def props = ["project_version"]

    def releaseVersion = setProperties(props, gradleFile)

    steps = [message { "Releasing a new version" },

             gitCheckChanges(),
             gitCheckBranch { "master" },

             // Set new Release version
             releaseVersion,
             checkProperties(props, gradleFile),

             // Commit and create release version
             gitAdd { [gradleFile] },
             gitCommit { "v" + releaseVersion.stepResult },

             task(":clean"),
             task(":test"),

             // Publish on bintray
             task(":bintrayUpload"),
             question {
                 "Publish artifact on jcenter ? \n\n" +
                         "https://bintray.com/tgirard12/kotlin/gradle-step-release-plugin ?"
             },

             gitPush("origin", "master"),

             githubRelease(),
             githubMilestone(),

             message { "Release v" + releaseVersion.stepResult + " OK :-)" }
    ]
}
